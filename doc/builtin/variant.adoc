= Variant

> In computer science, a tagged union, also called a variant [â€¦], is a data structure used to hold a value that could take on several different, but fixed, types.
Only one of the types can be in use at any one time, and a tag field explicitly indicates which one is in use.
-- Wikipedia contributors, https://en.wikipedia.org/wiki/Tagged_union

A possible variant value is called an _option_ in Onyx.
Option type ordering in a variant type is irrelevant, i.e. `Variant<stem:[tau], stem:[upsilon]>` stem:[-=] `Variant<stem:[upsilon], stem:[tau]>`.

A variant constructor accepts either a narrower variant instance or an instance of one of its options type (i.e. may be autocast).

.Variant construction
====
```nx
final x : Variant<Int32, String> = 42         # OK, accepts an option instance
final y : Variant<Int32, Float64, String> = x # OK, accepts a narrower variant
```
====

A variant option of type stem:[tau] may be extracted with either `stem:[tt "@get"]<stem:[tau]>` or `stem:[tt "@get?"]<stem:[tau]>` instrinsic invocation.
In contrary, the `stem:[tt "@neither"]<*stem:[tau]>` intrinsic throws if it *does* currently hold any type from stem:[tau].
The <<_safety, safety>> of invoking an intrinsic depends on the instance <<_storage, storage>>: a local variant instance has threadsafe access, otherwise it's fragile.

CAUTION: You can explicitly obtain a variant tag offset in bytes using the stem:[tt "@offsetof"] intrinsic.

.Variant intrinsics
====
```nx
import rand from "std/rand"

# TIP: The top-level scope is fragile.
#

final v = rand(42, "foo") : Variant<Int32, String>

final a = @get<Int32>(v) : Int32      # May throw `VariantError` if it's NOT `Int32`
final b = @get?<String>(v) : String?  # May return `nil` if it's NOT `String`
final c = @neither<Int32>(v) : String # May throw `VariantError` if it IS `Int32`

# Unsafely get a `String` option.
final d = unsafe! *((&v as void*)[@offsetof(v)] as String*)
```
====

If a result of a check whether a variant currently holds an option of type stem:[tau] is assigned to a local variable stem:[x] defined within a condition or a switcher, then the stem:[x] type is narrowed to stem:[tau].
A truthy-ness check applied to a variable defined in a condition also narrows down its type.

```onyx
final v0 = rand(42, "foo", nil)

if (let v1 = v0) {
  v0 : Variant<Int32, String, nil> # Intact
  v1 : Variant<Int32, String>      # Narrowed down to non-nilable
}

if (let v2 = @get?<Int32>(v0)) {
  v0 : Variant<Int32, String, nil> # Intact
  v2 : Int32                       # Narrowed down
}

switch (let v3 = v0) {
  case Int32 {
    v0 : Variant<Int32, String, nil> # Intact
    v3 : Int32                       # Narrowed down
  } else {
    v0 : Variant<Int32, String, nil> # Intact
    v3 : String?                     # Narrowed down (still nilable)
  }
}
```

Querying a variant at any scope transparently proxies the query to its actual option.
Therefore, _all_ of the options must implement the access for the compilation to succeed.

```onyx
final v0 = rand([1, 2], "foo", nil)

# v0.size() # => Panic! `nil.size` is undefined

if (let v1 = v0) {
  v1.size() # OK (both `Int32[2].size` and `String.size` are defined)
}

@neither<nil>(v0).size() # OK, may throw
```
